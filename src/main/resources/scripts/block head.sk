#
# Adds a command that allows player's to wear any kind of block as a hat.
# You could add a condition to allow only certain blocks to be worn if you want to.
#

command /head <material>:
	description: Wear a block as helmet/head
	aliases: hat
	usage: /head <block>
	permission: skript.hat
	trigger:
		if id of argument 1 is greater than 255:
			message "You cannot wear an item, only blocks"
			stop trigger
		if player doesn't have argument 1:
			if player doesn't have the permission "skript.hat.free":
				message "You do not have a %argument 1% you could wear!"
				stop trigger
		# Remove this condition if you want to override current helmet.
		# But it maybe causes player to loss helmet armor. Be careful.
		if helmet slot of player is not empty:
			message "You're already wearing a helmet!"
			stop trigger
		if player doesn't have the permission "skript.hat.free":
			remove argument 1 from player
		# This actually equips helmet slot of player with block specified in 1st argument
		equip player with argument 1